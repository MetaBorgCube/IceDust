module names-shortcuts

language icedust

fixture [[
  module inverses

  model
  
    entity Course{}
    
    [[...]]
]]

test Resolve shortcut [[
  entity Student{
    numCourses : Int = count(this.[[courses]])
  }
  
  relation Enrollment{
    Student *
    Course  +
    
    student.[[courses]] <-> course.students
  }
]] resolve #1 to #2

test Resolve invalid shortcut fails [[
  entity Student{
    numCourses : Int = [[count(this.iDoNotExist)]]
  }
  
  relation Enrollment{
    Student *
    Course  +
    
    student.courses <-> course.students
  }
]] >= 1 error at #1

test Resolve role in shortcut [[
  entity Student{
  }
  
  relation Enrollment{
    Student.y * -> [[student]]
    Course.x  + -> course
    
    student.abc <-> [[student]].bcd
  }
]] resolve #2 to #1

test Resolve invalid role in shortcut fails [[
  entity Student{
  }
  
  relation Enrollment{
    Student *
    Course  +
    
    [[student.abc <-> iDoNotExist.bcd]]
  }
]] >= 1 error at #1

test error on duplicate shortcut [[
  entity Node {}
  relation Edge {
    Node.c 1 -> a
    Node.d 1 -> b
    
    a.[[node]] <-> b.[[node]]
  }
  
config

  language features : first-class relations
]] 2 errors at #1,#2

test error on duplicate shortcut by desugaring [[
  entity Project{}
  entity Issue{}

  relation IssueProject{
    [[Issue]] 1
    [[Project]] *
  }
  
  relation ProjectIssue{
    [[Issue]] 1
    [[Project]] *
  }
]] >= 4 errors at #1,#2,#3,#4
