module names-traits-members

language icedust

fixture [[
  module test
  model
  [[...]]
]]

test resolve attribute subtype [[
  entity Assignment {
    [[name]] : String
  }

  entity AssignmentCollection extends Assignment {
    bla : String = [[name]]
  }
]] resolve #2 to #1

test unresolved member error [[
  entity Assignment {
  }

  entity AssignmentCollection extends Assignment {
    bla : String = [[name]]
  }
]] >= 1 error at #1

test resolve attribute subsubtype [[
  entity HasName {
    [[name]] : String
  }
  
  entity Assignment extends HasName {
  }

  entity AssignmentCollection extends Assignment {
    bla : String = [[name]]
  }
]] resolve #2 to #1

test resolve relation subtype [[
  entity Photo{
    name : String
  }
  
  entity HasPhoto{}
  
  relation HasPhoto.[[photo]] 1 <-> Photo
  
  entity Page extends HasPhoto {
    photoName : String = [[photo]].name
  }
]] resolve #2 to #1

test resolve relation attribute subtype [[
  entity Photo{
    [[name]] : String
  }
  
  entity HasPhoto{}
  
  relation HasPhoto 1 <-> Photo
  
  entity Page extends HasPhoto {
    photoName : String = photo.[[name]]
  }
]] resolve #2 to #1
