module names-inverses

language icedust2

fixture [[
  module inverses

  model
  
    entity Course{}
    
    [[...]]
]]

test Resolve inverse [[
  entity Student{
    numCourses : Int = count(this.[[enrollments]])
  }
  
  relation Enrollment{
    Student.[[enrollments]] * -> student
    Course.enrollments +      -> course
  }
]] resolve #1 to #2

test Resolve invalid inverse fails [[
  entity Student{
    numCourses : Int = [[count(this.iDoNotExist)]]
  }
  
  relation Enrollment{
    Student.enrollments * -> student
    Course.enrollments +  -> course
  }
]] >= 1 error at #1

test error on duplicate inverse [[
  entity Node {}
  relation Edge {
    Node.[[edge]] 1 -> a
    Node.[[edge]] 1 -> b
    a.b <-> b.a
  }
]] 2 errors at #1,#2

test error on duplicate inverse by desugaring [[
  entity Node {}
  relation Edge {
    [[Node]] 1
    [[Node]] 1
    
    node.c <-> node.d
  }
]] >= 2 errors at #1,#2 // always also results in duplicate shorcuts
