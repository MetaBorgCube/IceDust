module generating/java/data

imports // constructors

	java/Java
	signatures/Data-sig
	signatures/Expressions-sig
	signatures/Model-sig
	signatures/Modules-sig

imports // functions

  api/data-names-api
  api/model-names-api
  api/module-names-api
  generating/_ir/model
	generating/java/expressions
  names/naming/names
  naming/_notNaBL

rules
 
  data-modulename-to-java-stms(err): moduleName -> result
    with
      eiNames  := <modulename-get-entityinstancenames>moduleName;
      result1* := < map(entityinstancename-to-java-stm(err))>eiNames;
      result2* := <fmap(entityinstancename-to-java-stms2(err))>eiNames
    with
      result := bstm* |[
        ~result1*
        ~result2*
      ]|

rules

  entityinstancename-to-java-stm(err): x_eiName -> result
    with
      x_type := <_get-type>x_eiName
    with
      result := bstm |[
        x_type x_eiName = new x_type();
      ]|

  entityinstancename-to-java-stms2(err): x_eiName -> result
    with
      result1* := <entityinstancename-get-attributevaluenames;fmap(attributevaluename-to-java-stms(err))>x_eiName;
      result2* := <entityinstancename-get-relationvaluenames ;fmap(relationvaluename-to-java-stms (err))>x_eiName;
      result3* := <entityinstancename-get-rolevaluenames     ;fmap(relationvaluename-to-java-stms (err))>x_eiName;
      result4* := <entityinstancename-get-inversevaluenames  ;fmap(relationvaluename-to-java-stms (err))>x_eiName
    with
      result := bstm* |[
        ~result1*
        ~result2*
        ~result3*
        ~result4*
      ]|

rules
  
  attributevaluename-to-java-stms(err): x_attributevalueName -> result
    where
      membervaluename-is-attribute
    with
      x_eiName := <membervaluename-get-entityinstancename>x_attributevalueName;
      x_setter := <name-set>x_attributevalueName;
      valueAst := <membervaluename-get-expr>x_attributevalueName;
      (_, attributeValueJava) := <exp-to-java-stm(err|0)>valueAst
    with
      result := bstm* |[
        x_eiName.x_setter(~attributeValueJava);
      ]|
      
  relationvaluename-to-java-stms(err): x_relationvalueName -> result
    where
      membervaluename-is-relation <+ membervaluename-is-role <+ membervaluename-is-inverse
    with
      x_eiName   := <membervaluename-get-entityinstancename>x_relationvalueName;
      valueNames := <membervaluename-get-values>x_relationvalueName;
      result     := <map(relationvaluenameitem-to-java-stm(err|x_eiName, x_relationvalueName))>valueNames

rules

  relationvaluenameitem-to-java-stm(err|x_eiName, relationName): x_eiName2 -> result
    with
      x_set := <name-set>relationName;
      x_add := <name-add>relationName;
      if <get-member-origin;get-multiplicity;upper-one>relationName then
        result := java:block-stm |[
          x_eiName.x_set(x_eiName2);
        ]|
      else
        result := java:block-stm |[
          x_eiName.x_add(x_eiName2);
        ]|
      end