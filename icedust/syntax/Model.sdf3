module Model

imports
  Common
  Expressions
  Types

context-free syntax // entities

  Model.Entity = <
    entity <ID> {
      <{Member "\n"}*>
    }
  >
  Model.Entity = <
    entity <ID> extends <{Extends ", "}*> {
      <{Member "\n"}*>
    }
  >
  Model.Interface = <
    interface <ID> {
      <{Member "\n"}*>
    }
  >
  Model.Interface = <
    interface <ID> extends <{Extends "\n"}*> {
      <{Member "\n"}*>
    }
  >
  Model.FirstClassRelation = <
    relation <ID> {
      <{Member "\n"}*>
    }
  >
  
  Extends.Extends = <<ID>>

context-free syntax // shortcut relations

  Model.ShortcutRelation = [
    relation [EntityRef].[ID][SpaceMultiplicity?][SpaceOrdering?] <->[SpaceMultiplicity?][SpaceOrdering?] [EntityRef].[ID] {
      [{Attribute "\n"}*]
    }
  ]

context-free syntax // non-first-class relation

  Model.Relation = [
    relation [EntityRef][DotID?][SpaceMultiplicity?][SpaceOrdering?] <->[SpaceMultiplicity?][SpaceOrdering?] [EntityRef][DotID?]
  ]
  
  SpaceMultiplicity             = [ [Multiplicity]]
  SpaceOrdering                 = [ [Ordering]]

context-free syntax // entity members

  Member                        = <<Attribute>>
  Member                        = <<Role>>
  Member                        = <<Shortcut>>

  Attribute.Attribute           = <
    <ID> : <PrimitiveType><Multiplicity?>
  >
  Attribute.DerivationAttribute = <
    <ID> : <PrimitiveType><Multiplicity?> = <Exp>
  >
  Attribute.DefaultAttribute    = <
    <ID> : <PrimitiveType><Multiplicity?> = <Exp> (default)
  >
  DerivationType.DefaultValue   = <(default)>
  DerivationType.Derivation     = <>

  Role.Role                     = <<ID><DotID?><SpaceMultiplicity><SpaceOrdering?><SpaceArrowID?>>
  DotID                         = <.<ID>>
  SpaceArrowID                  = [ -> [ID]]

  Shortcut.Shortcut             = <<RoleRef>.<ID> \<-\> <RoleRef>.<ID>>

context-free syntax // specific references
  
  EntityRef.EntityRef           = ID
  AttributeRef.AttributeRef     = ID
  RoleRef.RoleRef               = ID
  MemberRef.MemberRef           = ID // attr or shortcut
