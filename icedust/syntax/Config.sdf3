module Config

imports

  Common

context-free syntax

  Config.JavaBackend = <
    backend : Java
      <Phase?>
  >
  
  Phase.Generate = <phase : generate>
  Phase.Compile  = <phase : compile>
  Phase.Execute  = <phase : execute>
  
context-free syntax
  
  Config.WebDSLBackend = <
    backend : WebDSL
    <{WebdslOption "\n"}*>
  >
  
  WebdslOption.UI = <ui : <UI>> 
  UI.None = <None>
  UI.ModelExplorer = <
    Model Explorer
      <TierSplit?>
  >
  TierSplit.None        = <tiersplit : None>
  TierSplit.IntraObject = <tiersplit : Object>

  WebdslOption.Embedded = <embedded : <Embedded>>
  Embedded.None     = <no>
  Embedded.Embedded = <yes>
  
  WebdslOption.DontGenerate  = <dont generate <ID>.<ID> <{DontGenerate ", "}*>>
  DontGenerate.Field         = <field>
  DontGenerate.Expression    = <expression>
  DontGenerate.IncomingPaths = <incoming paths>
  
  WebdslOption.EventualThreads  = <eventual threads: <INT>>
  WebdslOption.EventualInterval = <eventual interval: <INT> ms>
  
  WebdslOption.LogEventualUpdate = <log eventual update : <LogEventualUpdate>>
  LogEventualUpdate.None              = <no>
  LogEventualUpdate.LogEventualUpdate = <yes>
  
  WebdslOption.LogEventualStatus = <log eventual status : <LogEventualStatus>>
  LogEventualStatus.None              = <no>
  LogEventualStatus.LogEventualStatus = <yes>
  
context-free syntax

  Config.JavascriptBackend = <
    backend : Javascript
    <BackendPhase?>
    <BackendTarget?>
  >
 
  BackendPhase.Generate = <phase : generate>
  BackendPhase.Execute = <phase : execute>
  BackendPhase.Eval = <phase: eval<EvalOptions>>
  
  EvalOptions.Options = <(<EvalOption*>)>
  EvalOptions.NoOptions = <>
  EvalOption.Overwrite = <overwrite>
  
  BackendTarget.JavascriptTarget = <target: javascript>
  BackendTarget.HtmlTarget = <target: html <ResolveRuntime?>>
  
  ResolveRuntime.ResolveInline =     <runtime: inline>
  ResolveRuntime.ResolveFromSource = <runtime: "<STRING>">
  
context-free syntax
  
  Config.LanguageFeatures = <
    language features : <{LanguageFeature ", "}*>
  >
  
  LanguageFeature.DerivedRelations    = <derived relations>
  LanguageFeature.EventualCalculation = <eventual calculation>
  LanguageFeature.FirstClassRelations = <first-class relations>
  LanguageFeature.Inlines             = <inlines>
  LanguageFeature.Strategies          = <strategies>
  LanguageFeature.SubTyping           = <subtyping>
  
context-free syntax

  Config.LogIncremental = <log incremental : <LogIncremental>>
  LogIncremental.None           = <no>
  LogIncremental.LogIncremental = <yes>
  