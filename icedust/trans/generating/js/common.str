module generating/js/common

imports

  signatures/Types-sig
  api/model-names-api  
  api/types-api

rules


  
  exp-empty = upper-one ; exp-empty-one
  exp-empty = upper-many ; exp-empty-many
  exp-empty-one = !js-exp |[null]|
  exp-empty-many = !js-exp |[Runtime.EMPTY_ARRAY]|
  
  exp-attribute-empty: attribute -> js-exp |[undefined]|
    where
      name-is-derived 
    
  exp-attribute-empty = name-get-multiplicity ; exp-empty
      
    
  exp-collection : exp_elements* -> js-exp |[
    [exp_elements*] 
  ]|
        
  simplify-multiplicity : ZeroOrOne() -> One()
  simplify-multiplicity : OneOrMore() -> ZeroOrMore()
  simplify-multiplicity : ZeroOrMoreOrdered() -> <simplify-multiplicity> ZeroOrMore()
  simplify-multiplicity : OneOrMoreOrdered() -> <simplify-multiplicity> OneOrMore()
  simplify-multiplicity = id 
  
  
  lub-simple: (One(), ZeroOrOne()) -> ZeroOrOne()
  lub-simple: (ZeroOrOne(), One()) -> ZeroOrOne()
  lub-simple = (upper-many, id) ; !ZeroOrMore()
  lub-simple = (id, upper-many) ; !ZeroOrMore()
  lub-simple: (x, x) -> x
  
  with-prefix: ("", str) -> str
  with-prefix: (prefix, str) -> $[[prefix]_[str]]
  
  