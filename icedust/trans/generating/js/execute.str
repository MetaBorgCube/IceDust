module generating/js/execute

imports

  api/module-names-api
  
  generating/js/expression

rules

  generate-execute: x_moduleName -> js-stmt* |[
    function execute(state, x_scope){
      var x_result = [];
      stmts_expressions
      return {
        state: state,
        x_result: x_result
      };
    }
    module.exports.execute = execute;
  ]|
    with
      x_result := "result"
    ; x_scope := "ids"
    ; expressions := <modulename-get-exprs> x_moduleName
    ; stmts_expressions := <map(generate-execute-expression-stmt(|x_result, x_scope))> expressions
  
  generate-execute-expression-stmt(|x_result, x_scope): e -> js-stmt |[
    {
      var x_var = x_scope;
      stmts_e
      x_result = x_result.concat([exp_ref]);
    }
  ]|
    with
      x_var := <int-to-varname> 0
    ; (exp_ref, stmts_e, _) := <exp-to-js-stmts(|1)> e
  
  
  
  