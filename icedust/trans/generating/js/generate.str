module generating/js/generate

imports
  
  generating/js/model
  generating/js/init
  
  
  libstratego-gpp
  lib/js/pp/spoofaxJS-pp
  lib/js/pp/spoofaxJS-parenthesize
  

rules
  generate-javascript-code(err): moduleName -> (result, errors)
    with
      stmt_model* := <generate-model-statements> moduleName
      ; stmt_imports* := <generate-imports>
      ; stmt_init* := <generate-init> moduleName
      ; program := js-program |[
            stmt_imports*
            stmt_init*
            stmt_model*
        ]|
      ; result := <parenthesize-spoofaxJS
        ; prettyprint-spoofaxJS
        ; !V([], <id>)
        ; box2text-string(|120)> program
      ; errors := [] 
  
  
  generate-imports = !js-stmt* |[
    import { List, Set, Map } from 'immutable';
    import _ from 'lodash';
    import moment from 'moment';
    import { 
      sum, 
      min,
      max,
      avg,
      concat,
      count,
      conj,
      disj
    } from 'lib/expression';
    import {
      EMPTY_ARRAY
    } from 'lib/runtime';
  ]|
  