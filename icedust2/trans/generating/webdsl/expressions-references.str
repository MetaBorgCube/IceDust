module generating/webdsl/expressions-references

imports // constructors

  webdsl/constructors
  webdsl/WebDSL
  signatures/Expressions-sig
  tiersplit/constructors

imports // functions

  api/_runtime
  api/data-names-api
  api/model-names-api
  generating/_ir/model
  generating/webdsl/types

rules // member access
      
  expr-to-webdsl-expr(err): MemberAccess(ex1, x_name) -> webdsl:e |[ x_srt.x_getter(e1) ]|
    with
      e1       := <expr-to-webdsl-expr(err)>ex1;
      x_getter := <name-get>x_name;
      x_srt    := <get-type2>ex1

rules // direct references

  expr-to-webdsl-expr(err): Ref(x_entityName) ->  webdsl:e |[ x_entityName ]|
    where
      <is-entityinstancename>x_entityName

  expr-to-webdsl-expr(err): ex1@Ref(x_name) -> webdsl:e |[  x_srt1.x_getter(this)  ]|
    with
      x_getter := <name-get>x_name;
      x_srt1   := <attributename-get-entityname>x_name

rules // references : this

  expr-to-webdsl-expr(err): This() -> webdsl:e |[this]|

rules // references : tiersplit

  expr-to-webdsl-expr(err): TierSplit(name) -> exp |[ x_name() ]|
    with
      x_name := <name-get> name
