module generate-joboffers

language icedust

fixture [[

module JobOfferTest [[...]]

model
  
  entity JobOffer{
    
    hourlyPay  : Int?
    monthlyPay : Int?
    yearlyPay  : Int?
    
    vacation   : Int = 80 (default) // 8 percent
    thirteenth : Int = 83 (default) // 8.3 percent
    
    // hoursPerWeek : Int = 40 (default)
    // vacationDays : Int = 24 (default)
    
    // workDays         : Int = 365 - 104 - 8 - vacationDays
    // workHoursPerYear : Int = workDays * hoursPerWeek /. 5
    
    hourlyBruto  : Int = monthlyBruto /. 157
    monthlyBruto : Int = (yearlyPay /. 12 <+ monthlyPay <+ hourlyPay * 157 <+ 0) * (1000+vacation+thirteenth) /. 1000
    yearlyBruto  : Int = monthlyBruto * 12
    
    hourlyNetto  : Int = yearlyNetto /. 365
    monthlyNetto : Int = yearlyNetto /. 12
    yearlyNetto  : Int = yearlyBruto - tax

    leaseCar           : Int?
    leaseCarPercentage : Int?
    
    leaseCarAddition   : Int = leaseCar * leaseCarPercentage /. 100 <+ 0
    lease              : Boolean = leaseCar > 0 && leaseCarPercentage > -1 <+ false
    
    yearlyTaxed : Int = yearlyBruto + leaseCarAddition
    
    slice1percentage : Int = 3625 // 36.25 percent
    slice2percentage : Int = 4200
    slice3percentage : Int = 4200
    slice4percentage : Int = 5200
    
    slice1boundrary : Int = 19645
    slice2boundrary : Int = 33363
    slice3boundrary : Int = 56531

    slice1 : Int = min(yearlyTaxed ++ slice1boundrary)
    slice2 : Int = min(yearlyTaxed - slice1 ++ slice2boundrary - slice1boundrary)
    slice3 : Int = min(yearlyTaxed - slice1 - slice2 ++ slice3boundrary - slice2boundrary)
    slice4 : Int = min(yearlyTaxed - slice1 - slice2 - slice3 ++ 0)
    
    slice1tax : Int = slice1 * slice1percentage /. 10000
    slice2tax : Int = slice2 * slice2percentage /. 10000
    slice3tax : Int = slice3 * slice3percentage /. 10000
    slice4tax : Int = slice4 * slice4percentage /. 10000
    
    box1tax : Int = slice1tax + slice2tax + slice3tax + slice4tax

    workTaxDiscount    : Int = 2097                                   // only true for 19.248 - 40.721    
    defaultTaxDiscount : Int = 2103 - (yearlyBruto - 19645) * 2 /. 100 // only true for 19.645 - 56.495
    
    tax : Int = box1tax - workTaxDiscount - defaultTaxDiscount
    
  }
  
data

  sideline : JobOffer {
    hourlyPay = 18
    vacation = 0
    thirteenth = 0
  }
  
  phdYear1 : JobOffer { monthlyPay = 2062 }
  phdYear2 : JobOffer { monthlyPay = 2403 }
  phdYear3 : JobOffer { monthlyPay = 2516 }
  phdYear4 : JobOffer { monthlyPay = 2638 }
  
  secondment : JobOffer { monthlyPay = 2350 thirteenth = 0 leaseCar = 14000 leaseCarPercentage = 14}
  
  consultant : JobOffer { monthlyPay = 2650 thirteenth = 0 leaseCar = 17000 leaseCarPercentage = 14}
  
execute

  consultant.monthlyNetto
]]

test Job Offers execute-java [[ (on-demand) ]] transform "SPT -> to Java -> Execute" to 
"1996
"

test Job Offers execute-java incremental [[ (incremental) ]] transform "SPT -> to Java -> Execute" to
"Updating JobOffer.leaseCarAddition: 7
Updating JobOffer.lease: 7
Updating JobOffer.slice1boundrary: 7
Updating JobOffer.slice1percentage: 7
Updating JobOffer.slice2boundrary: 7
Updating JobOffer.slice2percentage: 7
Updating JobOffer.slice3boundrary: 7
Updating JobOffer.slice3percentage: 7
Updating JobOffer.slice4percentage: 7
Updating JobOffer.thirteenth: 7
Updating JobOffer.vacation: 7
Updating JobOffer.workTaxDiscount: 7
Updating JobOffer.monthlyBruto: 7
Updating JobOffer.yearlyBruto: 7
Updating JobOffer.defaultTaxDiscount: 7
Updating JobOffer.hourlyBruto: 7
Updating JobOffer.yearlyTaxed: 7
Updating JobOffer.slice1: 7
Updating JobOffer.slice2: 7
Updating JobOffer.slice3: 7
Updating JobOffer.slice4: 7
Updating JobOffer.slice4tax: 7
Updating JobOffer.slice3tax: 7
Updating JobOffer.slice2tax: 7
Updating JobOffer.slice1tax: 7
Updating JobOffer.box1tax: 7
Updating JobOffer.tax: 7
Updating JobOffer.yearlyNetto: 7
Updating JobOffer.monthlyNetto: 7
Updating JobOffer.hourlyNetto: 7
1996
"
