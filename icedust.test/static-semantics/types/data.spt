module types-data

language icedust

fixture [[
  module test
  [[...]]
]]

test error on omitting required attribute [[
  model
  entity X{
    i : Int
  }
  data
  [[x]] : X {}
]] 1 error at #1

test error on omitting required attribute multiple [[
  model
  entity X{
    i : Int
    j : String
  }
  data
  [[x]] : X {}
]] 1 errors at #1

test no error on omitting optional attribute[[
  model
  entity X{
    i : Int ?
  }
  data
  x : X {}
]] 0 errors

test no error on omitting derived attribute[[
  model
  entity X{
    i : Int = 5
  }
  data
  x : X {}
]] 0 errors

test error on supplying derived attribute[[
  model
  entity X{
    i : Int = 5
  }
  data
  x : X {
    [[i]] = 5900
  }
]] 1 error at #1

test error on omitting required attribute nested [[
  model
    entity X{
      i : Int
    }
    relation X.y <-> X.z
  data
    x : X {
      i = 5
      y =
        [[x1]] {}
    }
]] 1 error at #1

test infer enitity instance type [[
  model
    entity X{
      i : Int
    }
    relation X.y <-> X.z
  data
    x : X {
      i = 5
      y =
        [[x1]] {i=4}
    }
]] run name-get-type-test on #1 to "X"

test infer enitity instance type 2 [[
  model
    entity X{
      i : Int
    }
    relation X.y <-> X.z {}
  data
    x : X {
      i = 5
      y =
        <rel { } >
        [[x1]] {i=4}
    }
]] run name-get-type-test on #1 to "X"

test infer relation instance type [[
  model
  
    entity X {
      i : Int 1
    }
  
    relation XYRelation {
      X.yRelation * -> z
      X.zRelation * -> y
      y.z <-> z.y
    }
  
  data
  
    x : X {
      i = 5
      y = < [[rel]] { } > x1 {
        i = 4
      }
    }
]] run name-get-type-test on #1 to "XYRelation"

test infer relation instance type 2 [[
  model
    entity X{
      i : Int
    }
    relation X.y <-> X.z {}
  data
    x : X {
      i = 5
      y =
        <[[rel]] { } >
        x1 {i=4}
    }
]] run name-get-type-test on #1 to "XYRelation"
