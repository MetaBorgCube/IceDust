module java/pp/interface/annotation-type
rules

  java-to-box :
    AnnoDec(head, decs) -> box |[ V hs=0 [~head ~decs'] ]|
    where
      <block-structure> (1, decs) => decs'
      
  java-to-box:
    AnnoDecHead(mods, Id(name))
      ->
    box |[
      V vs=0 [
        ~*annos
        H hs=1 [~*mods' KW["@interface"] ~lit:name ]
      ]
    ]|
    where
      <  split-fetch-keep(not(is-Java-Anno))
         ; \ (l1, t, l2)  -> (l1, [t | l2]) \
         
      <+ !(<map(is-Java-Anno)>, [])
      
      <+ !([], <id>)
      > mods => (annos, mods')

  java-to-box :
    AnnoMethodDec(mods, type, Id(name), default)
      ->
    box |[ H hs=1 [~*mods ~type ~lit:name "(" ")" ~*default' ";"] ]|
    where
      let default-to-boxes =
            \ None() -> [] \
          + \ Some(d) -> [d] \

       in <default-to-boxes> default => default'
      end

  java-to-box :
    DefaultVal(val) -> box |[ H hs=1 [KW["default"] ~val] ]|
