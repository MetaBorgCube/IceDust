module java/pp/literal

rules

  java-to-box: True()      -> box |[ KW["true"]    ]|
  java-to-box: False()     -> box |[ KW["false"]   ]|
  java-to-box: Null()      -> box |[ KW["null"]    ]|
  java-to-box: VoidClass() -> H hs=0 [KW["void"] "." KW["class"]]
  java-to-box: Class(type) -> H hs=0 [~type "." KW["class"]]

  java-to-box: Bool(b)  -> b
  java-to-box: Char(c)  -> box |[ H hs=0 ["'" ~c "'"] ]|

  java-to-box: Single(c) -> box |[ s ]|
    where <implode-string> [c] => s

rules

  java-to-box:
    NamedEscape(c) -> box |[ s ]|
      where <implode-string> ['\', c] => s

  java-to-box:
    UnicodeEscape(us, c1, c2, c3, c4) -> box |[ s ]|
      where <implode-string> ['\' | <conc> (us, [c1, c2, c3, c4])] => s

  java-to-box:
    OctaEscape1(c1) -> box |[ s ]|
      where <implode-string> ['\', c1] => s

  java-to-box:
    OctaEscape2(c1, c2) -> box |[ s ]|
      where <implode-string> ['\', c1, c2] => s

  java-to-box:
    OctaEscape3(c1, c2, c3) -> box |[ s ]|
      where <implode-string> ['\', c1, c2, c3] => s

rules

  java-to-box: String(parts) -> box |[ H hs=0 ["\"" ~*parts "\""] ]|
  java-to-box: Chars(s) -> box |[ s ]|

rules

  java-to-box: Float(f)  -> box |[ NUM[ ~S(f) ] ]|
  java-to-box: Deci(d)   -> box |[ NUM[ ~S(d) ] ]|
  java-to-box: Hexa(h)   -> box |[ NUM[ ~S(h) ] ]|
  java-to-box: Octa(o)   -> box |[ NUM[ ~S(o) ] ]|

strategies

  is-double-quote = 
    is-quote(?34)

  is-quote(c) =
    where( explode-string; ?[<c> | <last; c>] )

